plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

group 'de.dfki.mary'
version '0.2.0-SNAPSHOT'
description 'Forced alignment based on MaryTTS, Kaldi, MFA, and Docker'

ext.pluginId = 'de.dfki.mary.voicebuilding.marytts-kaldi-mfa'

gradlePlugin {
    plugins {
        maryttsKaldiMfaPlugin {
            id = pluginId
            implementationClass = 'de.dfki.mary.voicebuilding.MaryttsKaldiMfaPlugin'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'de.undercouch:gradle-download-task:3.3.0'
    compile 'org.m2ci.msp:jtgt:0.5.2'
    runtime 'de.dfki.mary:marytts-voicebuilding:0.1', {
        exclude module: 'groovy-all'
    }
    runtime 'de.dfki.mary:marytts-lang-en:5.2', {
        exclude module: 'groovy-all'
    }
    testCompile 'org.testng:testng:6.9.6'
}

test {
    useTestNG()
    testLogging {
        exceptionFormat = 'full'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

pluginBundle {
    website = 'http://mary.dfki.de'
    vcsUrl = 'https://github.com/marytts/gradle-marytts-kaldi-mfa-plugin'
    description = project.description
    tags = ['text-to-speech', 'kaldi', 'marytts', 'forced-alignment']

    plugins {
        pandocRevealPlugin {
            id = pluginId
            displayName = 'Gradle MaryTTS Kaldi MFA plugin'
        }
    }
}
